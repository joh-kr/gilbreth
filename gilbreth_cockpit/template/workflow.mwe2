module workflow.DomainmodelGenerator

import org.eclipse.emf.mwe.utils.*

var targetDir = "src-gen"
var fileEncoding = "UTF-8"
var featureModelPath = "src/webshop.xml"
var famaMetamodelPath = "model/feature-model-schema.xsd"

Workflow {
	component = de.uni_leipzig.iwi.gilbreth.acape.generator.WorkflowLogger{
		message = "TestMessage"
	}
	
		component = org.eclipse.xtend.typesystem.xsd.XMLReader{
		modelSlot = "featureModel"
		uri = featureModelPath
		metaModel = org.eclipse.xtend.typesystem.xsd.XSDMetaModel{
			schemaFile = famaMetamodelPath
			registerPackagesGlobally = true
		}
	}
	
	component = de.uni_leipzig.iwi.gilbreth.acape.generator.FeatureModelToACAPEComponent{
		inputModelSlot   = "featureModel" 
		outputModelSlot  = "acapeModel"
		featureModelPath = featureModelPath
	}
	
	component = org.eclipse.emf.mwe.utils.DirectoryCleaner{
		directory = targetDir
	}
	
	// Put code generation here
	component = org.eclipse.xpand2.Generator {
		expand = "templates::Template::main FOR acapeModel"
		outlet = {
			path = targetDir
		}
		fileEncoding = fileEncoding
	}
} 